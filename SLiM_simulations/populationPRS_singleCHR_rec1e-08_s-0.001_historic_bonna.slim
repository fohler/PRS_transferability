/// # Population Simulation for PRS Analysis
///

initialize() {
	
	setwd("/home/lfohler/PRS_transferability/s-0.001/orig_burn_in/"); // bonna
	
	initializeSLiMOptions(keepPedigrees = T);
	
	defineConstant("simID", getSeed());
	
	// dominance coefficient: additive, (almost) recessive, (incomplete) dominance
	// (0.0 -> recessive, 1.0 -> dominant)
	domCoef = c(0.5, 0.05, 0.8);
	defineConstant("h", domCoef[simID - 1]);
	defineConstant("s", -0.001);
	
	// m1 mutationtype: neutral
	initializeMutationType("m1", 0.5, "f", 0.0);
	// m2 mutationtype: deleterious
	initializeMutationType("m2", h, "f", s);
	
	// length of 22 chromosomes with recombination interspaces
	c_length = 1000 * 1e5;
	
	// genomic elements: g1 -> gene, g2 -> no gene
	initializeGenomicElementType("g1", c(1,2), c(8,1)); // ratio between neutral and deleterious mutation is 8:1
	initializeGenomicElementType("g2", 1, 1); // neutral mutations for non-coding
	
	m1.color = "pink";
	m2.color = "green";
	g1.color = "blue";
	g2.color = "lightblue";
	m2.mutationStackPolicy = "l";
	
	// starting positions of alternating genes and non-coding sections
	start = readFile("/home/lfohler/startSingleChr.txt");
	start = asInteger(start);
	defineConstant("START", start);
	
	// corresponding ending positions, genomic element types and mutation rates
	end = c(start[1:(length(start)-1)] - 1, sum(c_length));
	end = asInteger(end);
	defineConstant("END", end);
	
	genomicType = rep(0, length(start));
	mutationRate = rep(0.0, length(start)); // could be set directly, but commands are kept for easier adjustment
	
	for(i in seqAlong(start)){
		if (i % 2 == 0) {
			genomicType[i] = 2;
			mutationRate[i] = 1.2e-8; // also neutral mutations for non coding
		} else {
			genomicType[i] = 1;
			mutationRate[i] = 1.2e-8;
		}
	}
	
	// genomic types with specified mutation rates
	for(i in seqAlong(genomicType)){
		initializeGenomicElement(genomicType[i], start[i], end[i]);
	}
	initializeMutationRate(mutationRate, end);
	
	// constant recombination rate over all bp
	defineConstant("recRate", 1e-8);
	initializeRecombinationRate(recRate);
	
	// two sexes
	initializeSex("A");
}

///
/// **Demography:**

// ancestral African population
1 /* add p1 */ { sim.addSubpop("p1", 7310); }

10 /* log p1*/ {
	log = sim.createLogFile("sim_log_rec" + recRate + "_s" + s + "_h" + h + "_p1.txt", logInterval=10);
	log.addGeneration();
	log.addSubpopulationSize(p1);
	log.addCustomColumn("mutation load", "(sum(p1.individuals.countOfMutationsOfType(m2))) / p1.individualCount;");
	log.addCustomColumn("Watterson's theta", "calcWattersonsTheta(p1.genomes);");
	log.addCustomColumn("mean fitness", "mean(p1.cachedFitness(NULL));");
}

// Expand the African population to 14474
73105 /* expansion of p1*/ { p1.setSubpopulationSize(14474); }

// Split non-Africans from Africans and set up migration between them
76968 /* split: p2, log p2, FST and BR for p1&p2*/ {
	sim.addSubpopSplit("p2", 1861, p1);
	
	// sim log p2 (mutation load, watterson's theta)
	log = sim.createLogFile("sim_log_rec" + recRate + "_s" + s + "_h" + h + "_p2.txt", logInterval=10);
	log.addGeneration();
	log.addSubpopulationSize(p2);
	log.addCustomColumn("mutation load", "(sum(p2.individuals.countOfMutationsOfType(m2))) / p2.individualCount;");
	log.addCustomColumn("Watterson's theta", "calcWattersonsTheta(p2.genomes);");
	log.addCustomColumn("mean fitness", "mean(p2.cachedFitness(NULL));");
	
	// FST and burden ratio calculation p1 p2
	log = sim.createLogFile("FST_BR_rec" + recRate + "_s" + s + "_h" + h + "_p1p2.txt", logInterval=10);
	log.addGeneration();
	log.addCustomColumn("FST", "calcFST(p1.genomes, p2.genomes);");
	log.addCustomColumn("burden ratio", "((sum(p1.individuals.countOfMutationsOfType(m2))) / p1.individualCount)/((sum(p2.individuals.countOfMutationsOfType(m2))) / p2.individualCount);");
}

// Split p2 into European and East Asian subpopulations
78084 /* split: p3, log p3, FST and BR for p1&p3, p2&p3*/ {
	sim.addSubpopSplit("p3", 554, p2);
	p2.setSubpopulationSize(1032);  // reduce European size
	
	// sim log p3 (mutation load, watterson's theta)
	log = sim.createLogFile("sim_log_rec" + recRate + "_s" + s + "_h" + h + "_p3.txt", logInterval=10);
	log.addGeneration();
	log.addSubpopulationSize(p3);
	log.addCustomColumn("mutation load", "(sum(p3.individuals.countOfMutationsOfType(m2))) / p3.individualCount;");
	log.addCustomColumn("Watterson's theta", "calcWattersonsTheta(p3.genomes);");
	log.addCustomColumn("mean fitness", "mean(p3.cachedFitness(NULL));");
	
	
	// FST and burden ratio calculation p1 p3
	log = sim.createLogFile("FST_BR_rec" + recRate + "_s" + s + "_h" + h + "_p1p3.txt", logInterval=10);
	log.addGeneration();
	log.addCustomColumn("FST", "calcFST(p1.genomes, p3.genomes);");
	log.addCustomColumn("burden ratio", "((sum(p1.individuals.countOfMutationsOfType(m2))) / p1.individualCount)/((sum(p3.individuals.countOfMutationsOfType(m2))) / p3.individualCount);");
	
	// FST and burden ratio calculation p2 p3
	log = sim.createLogFile("FST_BR_rec" + recRate + "_s" + s + "_h" + h + "_p2p3.txt", logInterval=10);
	log.addGeneration();
	log.addCustomColumn("FST", "calcFST(p2.genomes, p3.genomes);");
	log.addCustomColumn("burden ratio", "((sum(p2.individuals.countOfMutationsOfType(m2))) / p2.individualCount)/((sum(p3.individuals.countOfMutationsOfType(m2))) / p3.individualCount);");

}

// Set up exponential growth in Europe and East Asia
// Where N(0) is the base subpopulation size and t = gen - 78084:
//    N(Europe) should be int(round(N(0) * e^(0.0038*t)))
//    N(East Asia) should be int(round(N(0) * e^(0.0048*t)))
78084:79024 /* expansion of p2 and p3*/ {
	t = sim.generation - 78084;
	p2_size = round(1032 * exp(0.0038 * t));
	p3_size = round(554 * exp(0.0048 * t));
	
	p2.setSubpopulationSize(asInteger(p2_size));
	p3.setSubpopulationSize(asInteger(p3_size));
}

79025 /* output VCF */ { // "gravel": 18000
	p1.genomes.outputVCF("out_79k_rec" + recRate + "_s" + s + "_h" + h + "_p1.vcf");
	p2.genomes.outputVCF("out_79k_rec" + recRate + "_s" + s + "_h" + h + "_p2.vcf");
	p3.genomes.outputVCF("out_79k_rec" + recRate + "_s" + s + "_h" + h + "_p3.vcf");
}

79025 /* output fitness and sex */ { // "gravel": 18000
	fitness_p1 = p1.cachedFitness(NULL);
	fitness_p2 = p2.cachedFitness(NULL);
	fitness_p3 = p3.cachedFitness(NULL);
	sex_p1 = p1.individuals.sex;
	sex_p2 = p2.individuals.sex;
	sex_p3 = p3.individuals.sex;
	
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p1.txt", "id, fitness, sex");
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p1.txt", "i" + 0:(p1.individualCount-1) + "," + fitness_p1 + "," + sex_p1, append = T);
	
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p2.txt", "id, fitness, sex");
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p2.txt", "i" + 0:(p2.individualCount-1) + "," + fitness_p2 + "," + sex_p2, append = T);
	
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p3.txt", "id, fitness, sex");
	writeFile("fitness_sex_79k_rec" + recRate + "_s" + s + "_h" + h + "_p3.txt", "i" + 0:(p3.individualCount-1) + "," + fitness_p3 + "," + sex_p3, append = T);
}
